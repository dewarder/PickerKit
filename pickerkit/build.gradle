apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'me.tatarka.retrolambda'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

ext {
    artifactVersion = '0.0.3'
    artifactName = 'pickerkit'
    siteUrl = 'https://github.com/dewarder/PickerKit'
    gitUrl = 'https://github.com/dewarder/PickerKit.git'
}

version = artifactVersion
group = 'com.dewarder'


android {
    compileSdkVersion 27
    buildToolsVersion "25.0.3"

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 27
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        targetCompatibility 1.8
        sourceCompatibility 1.8
    }
}

install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                name artifactName
                url siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'dewarder'
                        name 'Artem Glugovsky'
                        email 'dewarder@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    compile 'com.android.support:appcompat-v7:27.0.1'
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile 'com.android.support:recyclerview-v7:27.0.1'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.github.rahatarmanahmed:circularprogressview:2.5.0'
    compile 'com.sothree.slidinguppanel:library:3.4.0'

    compile 'com.annimon:stream:1.1.7'
    testCompile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

task classesJar(type: Jar) {
    from "$buildDir/intermediates/classes/release"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())

bintray {
    user = properties['bintray.user']
    key = properties['bintray.apikey']
    configurations = ['archives']
    publish = true
    pkg {
        repo = 'maven'
        name = "$group:$artifactName"
        userOrg = 'dewarder'
        licenses = ['Apache-2.0']
        vcsUrl = gitUrl
        version {
            name = artifactVersion
            desc = 'Add ability to limit attachments'
            released = new Date()
            vcsTag = "$artifactVersion"
        }
    }
}
repositories {
    mavenCentral()
}